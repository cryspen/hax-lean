import Lean
import Aeneas
import Hax.MissingLean

set_option autoImplicit true

open Lean Std.Do Elab Parser Tactic Meta Aeneas Std

theorem Triple.of_pure_postcondition {f : Result α} (h : ⦃⌜True⌝⦄ f ⦃PostCond.noThrow fun _ => ⌜p⌝⦄) : p := by
  match f with
  | .ok _ | .fail _ | .div => simp [Triple, WP.wp, PredTrans.apply] at h <;> exact h

theorem triple_in_hypothesis {f : Result α} {Q : α → Assertion _} (p : Prop)
    (h : ⦃ ⌜ True ⌝ ⦄ f ⦃ ⇓ r => Q r ⦄)
    (hp : ⦃ ⌜ True ⌝ ⦄ f ⦃ ⇓? r => Q r → ⌜ p ⌝ ⦄) :
    p := by
  have : ⦃ ⌜ True ⌝ ⦄ f ⦃ (⇓ r => Q r) →ₚ (⇓ r => ⌜ p ⌝) ⦄ := by
    apply Triple.of_entails_right _ hp
    constructor
    · intro a
      dsimp; simp
    · simp
  have := Triple.mp f h this
  simp only [SPred.and_nil, SPred.down_pure, and_self] at this
  have : ⦃ ⌜ True ⌝ ⦄ f ⦃ ⇓ r => ⌜ p ⌝ ⦄ := by
    apply Triple.of_entails_right _ this
    simp
  apply Triple.of_pure_postcondition this

initialize Lean.registerTraceClass `Hax.hax_mvcgen_at

register_option hax_mvcgen.warnings : Bool := {
  defValue := true
  descr := "Enable or disable warnings produced by hax_mvcgen"
}

def haxMvcgenAt (mainGoal : MVarId) (hyp : LocalDecl) (cfgStx : TSyntax `Lean.Parser.Tactic.optConfig) (argStx : Syntax) : TacticM (List MVarId) := do
  forallTelescope (cleanupAnnotations := true) (← instantiateMVars hyp.type) fun xs hbody => do

    trace `Hax.hax_mvcgen_at fun () => m!"hax_mvcgen at {mkFVar hyp.fvarId}: {mainGoal}"
    let hbody ← whnfR hbody
    unless hbody.isAppOfArity' ``Triple 7 do
      Lean.Meta.throwTacticEx `hax_mvcgen mainGoal (m!"Expected `Std.Do.Triple`, got {hbody}")

    -- Create an MVar `newHyp` of type `Prop` representing the expression that `hyp` will be
    -- replaced with. We will determin what `newHyp` states later.
    let newHyp ← mkFreshExprMVar (kind := .syntheticOpaque) (mkSort .zero)

    -- To prove `newHyp`, we apply `triple_in_hypothesis`, followed by `hax_mvcgen`.
    trace `Hax.hax_mvcgen_at fun () => m!"apply `triple_in_hypthesis`"
    let newHypProof ← mkFreshExprMVar newHyp
    let lem ← mkAppM ``triple_in_hypothesis #[newHyp, mkAppN (mkFVar hyp.fvarId) xs]
    let goals ← newHypProof.mvarId!.applyN lem 1
    let [goal] := goals
      | Lean.Meta.throwTacticEx `hax_mvcgen mainGoal
          (m!"Unexpected number of goals after `triple_in_hypothesis`: {goals}")
    let previousLctxSize ← goal.withContext do pure (← getLCtx).decls.size
    trace `Hax.hax_mvcgen_at fun () => m!"run `mvcgen`"
    let cfgStx : TSyntax `Lean.Parser.Tactic.optConfig :=
      Parser.Tactic.appendConfig cfgStx (← `(Lean.Parser.Tactic.optConfig| -trivial))
    let inner : TSyntax `tactic := ⟨Syntax.node .none ``Lean.Parser.Tactic.mvcgen
        #[Syntax.atom .none "mvcgen", cfgStx.raw, argStx]⟩
    let goals ← evalTacticAt inner goal

    -- We partition the resulting goals into `newHypGoals` and `sideGoals`: If the conclusion
    -- of a goal is exactly `newHyp`, then we put it into `newHypGoals`, otherwise into `sideGoals`.
    trace `Hax.hax_mvcgen_at fun () => m!"partioning into newHypGoals/sideGoals"
    let mut newHypGoals : Array MVarId := #[]
    let mut sideGoals : Array MVarId := #[]
    for goal in goals do
      let (_, goal) ← goal.withContext do goal.intros
      let target ← goal.withContext do instantiateMVars (← goal.getType)
      if target == newHyp then
        newHypGoals := newHypGoals.push goal
      else
        if (target.find? (· == newHyp)).isSome then
          Lean.Meta.throwTacticEx `hax_mvcgen mainGoal
            (m!"VC goal target contains but is not equal to the mvar: {target}")
        sideGoals := sideGoals.push goal

    -- For each `newHypGoal`, we collect the local decls `newDecls` that have been introduced
    -- by the `hax_mvcgen` call above.
    trace `Hax.hax_mvcgen_at fun () => m!"collect new declarations"
    let mut newDecls : Array (Array LocalDecl) := #[]
    for newHypGoal in newHypGoals do
      let lctx ← newHypGoal.withContext getLCtx
      let decls := (lctx.decls.toArray.drop previousLctxSize).filterMap id
      newDecls := newDecls.push decls

    -- For each newHypGoal `i`, build `newHypGoalProofsᵢ`:
    --   `fun (p : Prop) (f₁ : fType₁[p]) ... (fₙ : fTypeₙ[p]) => fᵢ newFVarsᵢ₁ ... newFVarsᵢₘ`
    -- where `fTypeⱼ` = `∀ newFVarsⱼ₁ ... newFVarsⱼₘ, p`
    -- All `newHypGoalProofs` have the same type: `∀ (p : Prop), fType₁ → ... → fTypeₙ → p`
    let mut newHypGoalProofs := #[]
    for i in [0:newHypGoals.size] do
      newHypGoals[i]!.withContext do
        trace `Hax.hax_mvcgen_at fun () => m!"build proofs {newDecls[i]!.map (mkFVar ·.fvarId)}"
      let newHypGoalProof ← newHypGoals[i]!.withContext do
        withLocalDeclD `p (mkSort .zero) fun p => do
          let fDeclsNamed ← (Array.range newDecls.size).mapM fun j => do
            let fType ← newHypGoals[j]!.withContext
              (mkForallFVars (newDecls[j]!.map (mkFVar ·.fvarId)) p)
            pure (Name.mkSimple s!"f{j + 1}", fun _ : Array Expr => pure fType)
          withLocalDeclsD fDeclsNamed fun fs => do
            mkLambdaFVars (#[p] ++ fs)
              (mkAppN fs[i]! (newDecls[i]!.filter (!·.isLet)|>.map (mkFVar ·.fvarId)))
      newHypGoalProofs := newHypGoalProofs.push newHypGoalProof

    -- Assign proofs to goals
    trace `Hax.hax_mvcgen_at fun () => m!"assign proofs to goals"
    if !newHypGoals.isEmpty then
      let newHypInst ← inferType newHypGoalProofs[0]!
      trace `Hax.hax_mvcgen_at fun () => m!"assign newHyp {newHypInst}"
      newHyp.mvarId!.assign newHypInst
      for i in [0:newHypGoals.size] do
        trace `Hax.hax_mvcgen_at fun () => m!"assign newHypGoal {i}"
        newHypGoals[i]!.assign newHypGoalProofs[i]!
    else
      trace `Hax.hax_mvcgen_at fun () =>  m!"hax_mvcgen at: no mvar VCs generated, only side conditions: {sideGoals}"
      if hax_mvcgen.warnings.get (← getOptions) then
        logWarning m!"hax_mvcgen at: no mvar VCs generated, only side conditions."

    -- Discharge side goals with `mvcgen`'s trivial discharger:
    trace `Hax.hax_mvcgen_at fun () => m!"discharge side goals"
    let sideGoalsList ← sideGoals.toList.flatMapM
      fun sideGoal => do evalTacticAt (←  `(tactic| mvcgen_trivial)) sideGoal

    if !sideGoalsList.isEmpty then
      trace `Hax.hax_mvcgen_at fun () => m!"hax_mvcgen at: nontrivial side goals generated: {sideGoalsList}"
      if hax_mvcgen.warnings.get (← getOptions) then
        logWarning m!"hax_mvcgen at: nontrivial side goals generated."

    -- TODO: if everything worked correctly, this should not happen:
    if newHypGoals.isEmpty then
      return [← mainGoal.clear hyp.fvarId]



    -- Replace old `hyp` with `newHyp`, using `newHypProof`.
    mainGoal.withContext do
      let fvar := mkFVar hyp.fvarId
      trace `Hax.hax_mvcgen_at fun () => m!"replace hypothesis {fvar}: {mainGoal}"
    let newHypProof ← mkLambdaFVars xs newHypProof
    mainGoal.withContext do
      trace `Hax.hax_mvcgen_at fun () => m!"newHypProof: {newHypProof}"
    let {mvarId, fvarId, ..} ← mainGoal.replace hyp.fvarId newHypProof
    let mainGoals ←
      if xs.size == 0 then
        let fvar := mkFVar fvarId
        mvarId.withContext do
          trace `Hax.hax_mvcgen_at fun () => m!"apply new hypothesis {fvar}: {mvarId}"
        let r ← mvarId.apply (mkFVar fvarId)
        r.mapM (·.tryClear fvarId)
      else
        pure [mvarId]
    return (mainGoals ++ sideGoalsList)

syntax (name := hax_mvcgen_at_hyp) "hax_mvcgen" optConfig
  (" [" withoutPosition((simpStar <|> simpErase <|> simpLemma),*,?) "] ")? "at" ident : tactic

@[tactic hax_mvcgen_at_hyp]
def elabHaxMvcgenAtHyp : Tactic := fun stx => do
  let cfgStx : TSyntax `Lean.Parser.Tactic.optConfig := ⟨stx[1]⟩
  let argStx := stx[2]
  let mainGoal ← getMainGoal
  mainGoal.withContext do
    let lctx ← getLCtx
    let .some hyp := lctx.findFromUserName? (Syntax.getId stx[4])
      | Lean.Meta.throwTacticEx `hax_mvcgen mainGoal (m!"Cannot find local assumption {stx[4]}")
    replaceMainGoal (← haxMvcgenAt mainGoal hyp cfgStx argStx)
